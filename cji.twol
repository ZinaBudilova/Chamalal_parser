Alphabet
  а б в г д е ж з и й к л м н о п р с т у х ч ц ш ъ ь I Ӏ ӏ 1 2 э %-

  %{д%}:д 
  %{н%}:н
  %{е%}:е
  %{а%}:а
  %{А%}:а   
  %{ва1%}:ва
  %{ва2%}:ва
  %{в%}:0
  %{у%}:у
  %{и%}:0
;

Rules

"н assimilation before д: акьбегьан-{д}а > акьбегьан-на"
%{д%}:н <=> %{н%}:н _ ;

"н turns to nasalization of previous vowel before ч: бегьан-чекIу > бегьа2-чекIу"
%{н%}:2 <=> _ ч ;

"е elision before б: ечI{е}-бегьанна > ечI-бегьанна"
%{е%}:0 <=> _ б ;

"е elision before а: ечI{е}-ацIада > ечI-ацIада"
%{е%}:0 <=> _ а ;

"а elision before б: анлI{а}-бегьанна > анлI-бегьанна"
%{а%}:0 <=> _ б ;

"а + а = а: анлI{а}-ацIада > анлI-ацIада"
%{а%}:0 <=> _ а ;

"а elision before д: гьакь-аз{а}-да > гьакь-аз-да"
%{а%}:0 <=> _ д ;

"а elision in the end of the word: боъаз{а} > боъаз"
%{а%}:0 <=> _ # ;

"а + а = а: лъалъ{А}-ацIада > лъалъ-ацIада"
%{А%}:0 <=> _ а ;

"ва turns to о before б: бо{ва1}-бегьанна > боо-бегьанна"
%{ва1%}:о <=> _ б ;

"ва + а = ва (1): бо{ва1}-ацIада > бов-ацIада"
%{ва1%}:в <=> _ а ;

"ва elision before б: акь{ва2}-бегьанна > акь-бегьанна"
%{ва2%}:0 <=> _ б ;

"ва + а = ва (2): акь{ва2}-ацIада > акь-ацIада"
%{ва2%}:в <=> _ а ;

"{в} turns to в before и: ар{в}-и > арв-и"
%{в%}:в <=> _ и ;

"{у} turns to и in some 4th class adjective forms: микI{и}-{у}л > микI-ил"
%{у%}:и <=> %{и%}:0  _ ;


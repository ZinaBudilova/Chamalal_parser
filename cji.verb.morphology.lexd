PATTERNS

# without class prefix
VerbInfStem[no_pref] [<verb><inf>:]
VerbPstStem[no_pref] [<verb><pst>:] ([<neg>:елъ]? | [<neg>:е2лъ]? | [<proh>:хъала]? | [<cond>:аада]? | [<cond>:ада]?)
VerbFutStem[no_pref] [<verb><fut>:] ([<neg>:ии]? | [<neg>:ии2]?)
VerbImpStem[no_pref] [<verb><imp>:]
CvbPstStem[no_pref] [<cvb><pst>:] ([<neg>:ида]? | [<neg>:инна]?)
CvbPrsStem[no_pref] [<cvb><prs>:]

# with class prefix
(VerbClassPrefixes VerbInfStem[-no_pref] [<verb><inf>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes VerbPstStem[-no_pref] [<verb><pst>:] ([<neg>:елъ]? | [<neg>:е2лъ]? | [<proh>:хъала]? | [<cond>:аада]? | [<cond>:ада]?))[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes VerbFutStem[-no_pref] [<verb><fut>:] ([<neg>:ии]? | [<neg>:ии2]?))[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes VerbImpStem[-no_pref] [<verb><imp>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes CvbPstStem[-no_pref] [<cvb><pst>:] ([<neg>:ида]? | [<neg>:инна]?))[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes CvbPrsStem[-no_pref] [<cvb><prs>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]

# participle
VerbPstStem[no_pref] ([<ptcp>:идда] | [<ptcp>:{д2}а]) [<pst>:]
VerbFutStem[no_pref] [<ptcp>:б]
CvbPrsStem[no_pref] [<ptcp>:исс] [<prs>:]
VerbInfStem[no_pref] [<ptcp>:л1аб] [<fut>:]

(VerbClassPrefixes VerbPstStem[-no_pref] ([<ptcp>:идда] | [<ptcp>:{д}а]) [<pst>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes VerbFutStem[-no_pref] [<ptcp>:б])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes CvbPrsStem[-no_pref] [<ptcp>:исс] [<prs>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]
(VerbClassPrefixes VerbInfStem[-no_pref] [<ptcp>:л1аб] [<fut>:])[^[a-pref,e-pref,ia-pref,i-pref,n-pref,u-pref]]

# linker
VerbLinker [<verb><prs>:]


LEXICON VerbLinker
ида  # is

LEXICON VerbClassPrefixes
<m.sg>:ва[a-pref]  # Bokarev's 1st group
<f.sg>:йа[a-pref]
<h.pl>:ба[a-pref]
<nh1.sg>:ба[a-pref]
<nh2.sg>:йа[a-pref]
<nh3.sg>:йа[a-pref]
<nh.pl>:йа[a-pref]
<m.sg>:ве[e-pref]  # Bokarev's 2nd group
<f.sg>:йе[e-pref]
<h.pl>:бе[e-pref]
<nh1.sg>:бе[e-pref]
<nh2.sg>:йе[e-pref]
<nh3.sg>:йе[e-pref]
<nh.pl>:йе[e-pref]
<m.sg>:ви[ia-pref]  # Bokarev's 3rd group
<f.sg>:йи[ia-pref]
<h.pl>:ба[ia-pref]
<nh1.sg>:би[ia-pref]
<nh2.sg>:йи[ia-pref]
<nh3.sg>:йи[ia-pref]
<nh.pl>:йа[ia-pref]
<m.sg>:ви[i-pref]  # Bokarev's 4th group
<f.sg>:йи[i-pref]
<h.pl>:би[i-pref]
<nh1.sg>:би[i-pref]
<nh2.sg>:йи[i-pref]
<nh3.sg>:йи[i-pref]
<nh.pl>:йи[i-pref]
<m.sg>:ви[n-pref]  # Bokarev's 5th group
<f.sg>:ни[n-pref]
<h.pl>:би[n-pref]
<nh1.sg>:ба[n-pref]
<nh2.sg>:ни[n-pref]
<nh3.sg>:ди[n-pref]
<nh.pl>:да[n-pref]
<m.sg>:ву[u-pref]  # Bokarev's 6th group
<f.sg>:йи[u-pref]
<h.pl>:ба[u-pref]
<nh1.sg>:бу[u-pref]
<nh2.sg>:йи[u-pref]
<nh3.sg>:ву[u-pref]
<nh.pl>:йа[u-pref]

